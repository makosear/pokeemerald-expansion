mapscripts LittlerootTown_SamsHouse_MapScripts {
     MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SAM_STATE, 2: LittlerootTown_Samshouse_Event_Script_SamHeart1
    ]
    MAP_SCRIPT_ON_TRANSITION: SamHeart1_Helper
}

script SamHeart1_Helper {
    setflag(FLAG_HIDE_LITTLEROOT_TOWN_SAMS_HOUSE_SEBASTIAN)
    if (var(VAR_SAM_STATE) == 1) { 
        setvar(VAR_SAM_STATE , 2)
    }
}

script LittlerootTown_SamsHouse_Event_Script_MeetSam {
    lock
    faceplayer

    if(!flag(FLAG_MEETSAM)) {
        playbgm(MUS_ENCOUNTER_BRENDAN, TRUE)
        msgbox ("Hey, I'm Sam. Good to meet you.", MSGBOX_NPC)
        setflag(FLAG_MEETSAM)
        fadedefaultbgm
    }

    elif (var(VAR_SAM_STATE) < 3) { 
        msgbox(format("I'm glad I have the day off! Sebastian and I have been meaning to hang for a while. Why don't you drop by later. {PLAYER}?"), MSGBOX_NPC)
        setvar(VAR_SAM_STATE, 1)
    }

    if (var(VAR_SAM_STATE) < 5 && var(VAR_SAM_STATE) > 2) {
        msgbox(format("Thanks for your help, {PLAYER}!\nI really think we're onto something."), MSGBOX_NPC)
    }

    releaseall
}

script LittlerootTown_SamsHouse_Event_Script_Vincent {
    lock
    faceplayer
    msgbox("Oh, hi! Name's Vincent!", MSGBOX_NPC)
    releaseall
}

script LittlerootTown_SamsHouse_Event_Script_Jodi {
    lock
    faceplayer
    msgbox (format("Oh! You aren't exactly how I imagined... but that's okay! I'm Jodi."), MSGBOX_NPC)
    msgbox ("Have you met my boys yet?", MSGBOX_NPC)
    releaseall
}

script LittlerootTown_Samshouse_Event_Script_SamHeart1 {
    lockall
    fadescreen(FADE_TO_BLACK)
    hideobjectat(1,MAP_LITTLEROOT_TOWN_SAMS_HOUSE)
    hideobjectat(3,MAP_LITTLEROOT_TOWN_SAMS_HOUSE)
    clearflag(FLAG_HIDE_LITTLEROOT_TOWN_SAMS_HOUSE_SEBASTIAN)
    addobject(4)
    fadescreen(FADE_FROM_BLACK)
    playbgm(MUS_OLDALE, TRUE)
    applymovement(OBJ_EVENT_ID_PLAYER, SamHeart1_Movement_Player)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    applymovement(4, Common_Movement_ExclamationMark)
    waitmovement(4)
    applymovement(4, Common_Movement_FaceLeft)
    waitmovement(4)
    waitmovement(2)
    delay(8)
    applymovement(2, SamHeart1_Movement_2)
    waitmovement(2)
    msgbox(format("SAM: Oh, hi {PLAYER}! Sebastian and I were just having a little 'jam session'."))
    delay(4)
    applymovement(4, Common_Movement_FaceUp)
    waitmovement(0)
    delay(8)
    applymovement(4, Common_Movement_FaceLeft)
    waitmovement(0)
    delay(8)
    msgbox(format("SAM: We're trying to start a band, but we still don't know what kind of music to make. There's too many possibilities."))

    delay(8)

    msgbox(format("SAM: Say, {PLAYER}... what kind of music do you like?"), MSGBOX_DEFAULT)

    dynmultichoice(8,4, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "Cheerful pop music.",
"Experimental noise rock.", "Hi-Energy dance music.", "Honky-tonky country music.")

    msgbox(format("SAM: Hey, you know what? If you win against me in a POKEMON battle, we'll play that kind of music."))

    msgbox(format("SAM: What do you say, Sebastian? Should we do this?"), MSGBOX_DEFAULT)

    delay(8)

    msgbox(format("SEBASTIAN: ...Okay."), MSGBOX_DEFAULT)

    msgbox(format("SAM: Alright, {PLAYER}. Just, psst, try not to make a mess in the house or my Mom will flip."))

    # trainerbattle_no_intro TRAINER_GRUNT_PETALBURG_WOODS, PetalburgWoods_Text_YoureKiddingMe

    trainerbattle_no_intro(TRAINER_SAM_ONEHEART, "Your buddy is pretty good!")

    msgbox(format("SAM: Thanks for the help, {PLAYER}. With my guitar skills, and Sebastian's wizardry on the synthesizer, we're gonna be a screaming success. I'm convinced of it!"))

    delay(16)

    msgbox(format("SAM: Now I just need to find someone to play drums..."), MSGBOX_DEFAULT)

    fadescreen(FADE_TO_BLACK)
    showobjectat(1,MAP_LITTLEROOT_TOWN_SAMS_HOUSE)
    showobjectat(3,MAP_LITTLEROOT_TOWN_SAMS_HOUSE)
    setflag(FLAG_HIDE_LITTLEROOT_TOWN_SAMS_HOUSE_SEBASTIAN)
    removeobject(4)
    fadescreen(FADE_FROM_BLACK)
    fadedefaultbgm
    setvar(VAR_SAM_STATE, 3)
    releaseall
}

movement SamHeart1_Movement_Player {
    walk_up
    walk_right * 6
    walk_up * 3
    face_right
}

movement SamHeart1_Movement_2 {
    walk_left
    face_down
}

script LittlerootTown_Samshouse_Event_Script_SamHeart2 {
    lockall
    fadescreen(FADE_TO_BLACK)
    //set player xy to 17, 3
    clearflag(FLAG_HIDE_ROUTE_136_SAMTWOHEARTS)
    addobject(1)
    addobject(2)
    addobject(3)
    setobjectxyperm (OBJ_EVENT_ID_PLAYER, 17, 3)
	setobjectmovementtype (OBJ_EVENT_ID_PLAYER, MOVEMENT_TYPE_FACE_DOWN)
    fadescreen(FADE_FROM_BLACK)
    playbgm(MUS_OLDALE, TRUE)

    #vincent playing with his toys on the sand
    #sam watching from afar
    #player walks up from the top of the screen
    #sam and player face each other
    applymovement(OBJ_EVENT_ID_PLAYER, SamHeart2_Movement_Player)
    waitmovement(0)

    applymovement(2, Common_Movement_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    waitmovement(0)
    waitmovement(2)

    msgbox(format("SAM: Hello {PLAYER}. Nice day, isn't it?"), MSGBOX_DEFAULT)

    #sam and player look up
    applymovement(2, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    waitmovement(2)
    waitmovement(0)

    msgbox(format("SAM: I'm just keeping an eye on my little brother. It's good for him to get some fresh air on a day like this."), MSGBOX_DEFAULT)

    msgbox(format("SAM: Vince wanted to go to the woods to find some Bug Pokemon, but Mom said it was too dangerous. He always gets lost..."), MSGBOX_DEFAULT)

    msgbox(format("SAM: I didn't want to bum him out, though..."), MSGBOX_DEFAULT)

    #sam looks at player, after delay, player looks at sam
    applymovement(2, Common_Movement_FaceLeft)
    waitmovement(0)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)

    msgbox(format("SAM: Hey, {PLAYER}! Do you think you can do me a favor?"), MSGBOX_YESNO)

    while (var(VAR_RESULT) != 1) {
        msgbox("...Please?", MSGBOX_YESNO)
    }

    msgbox(format("SAM: Just follow along."), MSGBOX_DEFAULT)

    #sam walks to vincent, looks at him, then vincent looks at sam
    applymovement(2, SamHeart2_Movement_2)
    waitmovement(2)
    applymovement(2, Common_Movement_FaceRight)
    waitmovement(0)
    applymovement(1, Common_Movement_FaceLeft)
    waitmovement(1)

    msgbox(format("SAM: Hey, Vince! {PLAYER} wants to battle you!"), MSGBOX_DEFAULT)

    #vincent does a lil happy jump and looks up, at player
    applymovement(1, SamHeart2_Movement_1) 
    waitmovement(1)
    applymovement(1, Common_Movement_LookUp)
    waitmovement(1)

    msgbox(format("VINCENT: Really? I want to battle him too!"), MSGBOX_DEFAULT) 
    
    applymovement(1, Common_Movement_FaceLeft)
    msgbox(format("Will you help me out, Sam?"), MSGBOX_DEFAULT)

    msgbox(format("SAM: Huh? Oh, like a double battle?"), MSGBOX_DEFAULT)

    applymovement(2, Common_Movement_LookUp)
    waitmovement(2)
    msgbox(format("SAM: What do you say, {PLAYER}?"), MSGBOX_YESNO)

    while (var(VAR_RESULT) != 1) {
        msgbox("C'mon, man, just this once...", MSGBOX_YESNO)
    }

    playbgm(MUS_ENCOUNTER_INTENSE, TRUE)

    msgbox(format("SAM: Alright, {PLAYER}. Let's do this!"), MSGBOX_DEFAULT)

    applymovement(1, SamHeart2_Movement_1)
    waitmovement(1)
    applymovement(1, Common_Movement_FaceUp)
    waitmovement(1)

    playbgm(MUS_ENCOUNTER_MALE, TRUE)

    msgbox(format("VINCENT: I'm ready!"), MSGBOX_DEFAULT)

    trainerbattle_two_trainers(TRAINER_SAM_ONEHEART, "Man...", TRAINER_VINCENT_SAMTWOHEART, "That was fun!\n I wanna be as strong as you one day, Mister!")

    playbgm(MUS_OLDALE, TRUE)

    msgbox(format("SAM: You really won against the both of us, huh..."), MSGBOX_DEFAULT)

    msgbox(format("VINCENT: That was so cool!"), MSGBOX_DEFAULT)

    playbgm(MUS_DUMMY, TRUE)

    # vincent walks back to his toys
    applymovement(1, Common_Movement_FaceDown)

    msgbox(format("SAM: Thanks for humoring me, {PLAYER}. That was cool of you."), MSGBOX_DEFAULT)

    # sam and player face forwards and delay
    applymovement(2, Common_Movement_FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
    waitmovement(2)
    waitmovement(0)
    delay(16)


    msgbox(format("SAM: ..."), MSGBOX_DEFAULT)

    # delay
    delay(16)

    msgbox(format("SAM: Our Dad used to take us to the beach on sunny days like this."), MSGBOX_DEFAULT)

    # vince is startled, jumps and walks to sam, real close face each other 

    applymovement(1, SamHeart2_Movement_1_2)
    waitmovement(1)

    msgbox(format("VINCENT: Sam...?"), MSGBOX_DEFAULT)

    msgbox(format("VINCENT: Is Dad ever going to come home?"), MSGBOX_DEFAULT)

    # sad song starts, melancholic but soft 

    # MUS_LILYCOVE_MUSEUM is a choice
    # MUS_VERDANTURF is also good
    # MUS_CREDITS might be my choice if it doesnt out of nowhere gets fast

    playbgm(MUS_END, TRUE)

    # common movement tear drop mark 
    #applymovement(1, Common_Movement_TearDropMark) #TODO ADD THIS

    playse(SE_M_DIZZY_PUNCH)
    applymovement(2, Common_Movement_ExclamationMark) #PLACEHOLDER

    msgbox(format("SAM: Of course, Vince. Dad's going to come back as soon as he possibly can."), MSGBOX_DEFAULT)

    msgbox(format("VINCENT: But I heard some grown-ups saying that... that all of our troops are getting shot to pieces."), MSGBOX_DEFAULT)

    # common movement exclamation mark 
    playse(SE_PIN)
    applymovement(2, Common_Movement_ExclamationMark)
    waitmovement(2)

    msgbox(format("SAM: Don't believe everything you hear!"), MSGBOX_DEFAULT)

    msgbox(format("SAM: Don't you remember how strong Dad is? He'd never let a pathetic Gotoro grunt get him like that! Don't you worry!"), MSGBOX_DEFAULT)

    msgbox(format("SAM: And always remember that Mom and I are here to play with you if you ever feel lonely."), MSGBOX_DEFAULT)

    msgbox(format("Okay!"), MSGBOX_DEFAULT)

    playbgm(MUS_OLDALE, TRUE)

    # vince jumps, returns to his toys
    applymovement(1, SamHeart2_Movement_2) #can reuse this movement

    msgbox(format("SAM: To be honest, I have no idea if my old man will come back... We've all read the reports. Our soldiers are falling by the thousands.\nBut what am I going to say to Vincent?"), MSGBOX_DEFAULT)

    dynmultichoice(8,4, TRUE, 6, 0, DYN_MULTICHOICE_CB_NONE, "It's best to be honest with kids.", "You did the right thing. Kids should have hope.")

    # common movement ... mark
    # applymovement(2, Common_Movement_EllipsisMark) #TODO

    msgbox(format("SAM: You think so?"), MSGBOX_DEFAULT)

    if (var(VAR_RESULT) == 1) {
        msgbox(format("SAM: Maybe you're right... The world is a cruel place, but kids have to learn that one way or another."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("SAM: Yeah... kids should play and have fun while they still have the chance."), MSGBOX_DEFAULT)
    }

    # sam faces player 
    # small delay and then player faces sam
    applymovement(2, Common_Movement_FaceLeft)
    waitmovement(2)
    delay(16)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    msgbox(format("SAM: Hey did I tell you the band's starting to come together?"), MSGBOX_DEFAULT)

    msgbox(format("SAM: Yeah! It turns out Abigail's a pretty good drummer. And Sebastian seemed to really want her in the band..."), MSGBOX_DEFAULT)

    fadescreen (FADE_TO_BLACK)
    fadescreen (FADE_FROM_BLACK)
}

movement SamHeart2_Movement_Player {
    walk_down * 4
}

movement SamHeart2_Movement_2 {
    walk_down * 3
}

movement SamHeart2_Movement_1 {
    jump_in_place_left
}

movement SamHeart2_Movement_1_2 {
    jump_in_place_down
    walk_up * 3
    face_left
}